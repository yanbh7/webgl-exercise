uniform vec2 uResolution;
uniform float uSize;
uniform float uProgress;
uniform vec3 uColorA;
uniform vec3 uColorB;

attribute vec3 aPositionTarget;
attribute float aSize;
varying vec3 vColor;
#include /src/common/glsl/simplex3dNoise.glsl

void main()
{
    // noise
    float noiseOrigin = simplex3dNoise(position * 0.2);
    float noiseTarget = simplex3dNoise(aPositionTarget * 0.2);
    float noise = mix(noiseOrigin, noiseTarget, uProgress);
    noise = smoothstep(-1.0, 1.0, noise);

    float duration = 0.6;
    float start = (1.0 - duration) * noise;
    float end = start + duration;

    // Mixed position
    float progress = smoothstep(start, end, uProgress);
    vec3 mixedPosition = mix(position, aPositionTarget, progress);
    // Final position
    vec4 modelPosition = modelMatrix * vec4(mixedPosition, 1.0);
    vec4 viewPosition = viewMatrix * modelPosition;
    vec4 projectedPosition = projectionMatrix * viewPosition;
    gl_Position = projectedPosition;

    // Point size
    gl_PointSize = aSize * uSize * uResolution.y;
    gl_PointSize *= (1.0 / - viewPosition.z);

    // varyings
    vColor = mix(uColorA, uColorB, noise);
}